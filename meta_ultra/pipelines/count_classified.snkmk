import meta_ultra.api as api


def count_classified_output(wildcards):
	tools = config['TOOLS_TO_RUN']
	inp = []
	dataSamplePairs=[]
	for sampleName, dataRecords in config['SAMPLES'].items():
		for dataName, dataConf in dataRecords.items():
			dataType = DataType.asDataType(dataConf['DATA_TYPE'])
			if dataType in [DataType.WGS_DNA_SEQ_SINGLE_END, DataType.WGS_DNA_SEQ_PAIRED_END]:
				dataSamplePairs.append( (sampleName, dataConf['DATA_NAME']))

	if 'COUNT_CLASSIFIED' in tools:
		pattern = config['OUTPUT_DIR']+'{sample}.{data_name}.reads_classified.json.registered'
		for sample, data in dataSamplePairs:
			inp.append( pattern.format(sample=sample, data_name=data))
	return inp


output_builders.append( count_classified_output)

rule count_classified_reads_single:
	input:
		host=getHostReads,
		nothost=getNotHostReads,
		krakenmpa=config['OUTPUT_DIR']+'{sample}.{data_name}.' + config['KRAKEN']['MPA_EXT']
	output:
		main=config['OUTPUT_DIR']+'{sample}.{data_name}.reads_classified.json'
	params:
		script=config['COUNT_CLASSIFIED']['EXC']['FILEPATH'],
		job_name=config['JOB_NAME_PREFIX']+'count_classified_reads_single_{sample}_{data_name}'
	resources:
		time=1,
		n_gb_ram=2
	threads: 1
	shell:
		'{params.script} {input.host[0]} {input.nothost[0]} {input.krakenmpa} > {output.main}'

localrules: register_count_classified_reads_single
                
rule register_count_classified_reads_single:
	input:
		inp=config['OUTPUT_DIR']+'{sample}.{data_name}.reads_classified.json'
	output:
		flag=config['OUTPUT_DIR']+'{sample}.{data_name}.reads_classified.json.registered'
	params:
		conf_name=config['NAME'],
		sample_name='{sample}',
		data_name='{data_name}',
		exp_name=lambda wc: config['SAMPLES'][wc.sample][wc.data_name]['EXPERIMENT_NAME'],
		proj_name=lambda wc: config['SAMPLES'][wc.sample][wc.data_name]['PROJECT_NAME'],
	run:
		module = 'classify_reads'
		name = '{}_{}'.format(params.data_name, module)
		try:
			api.saveResult( name,
				module,
				[input.inp],
				params.data_name,
				params.conf_name,
				params.sample_name,
				params.exp_name,
				params.proj_name
			)
		except RecordExistsError:
			pass
		open(output.flag,'a').close()










